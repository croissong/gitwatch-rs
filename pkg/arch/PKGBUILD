# Maintainer: Jan MÃ¶ller <jan.moeller0@pm.me>

pkgname=gitwatch-rs
pkgver=1.0.0
pkgrel=1
pkgdesc="CLI to watch a git repo and automatically commit changes"
arch=('x86_64')
url="https://github.com/croissong/gitwatch-rs"
license=('MIT')
depends=('libgit2' 'openssl')
makedepends=('rust' 'cargo' 'pkg-config')
provides=('gitwatch')
conflicts=('gitwatch')

source=("$pkgname-$pkgver.tar.gz::https://github.com/croissong/gitwatch-rs/archive/v$pkgver.tar.gz")
sha256sums=('0b277fd5e8b44ba7e0259c2de0a2d47837708828080701b7fdc25f93dde8d6ef')

prepare() {
  cd "$pkgname-$pkgver"
  cargo fetch --locked
}

build() {
  cd "$pkgname-$pkgver"
  cargo build --release --frozen
}

check() {
  cd "$pkgname-$pkgver"
  cargo test --frozen
}

package() {
  cd "$pkgname-$pkgver"

  # Install binary
  install -Dm755 "target/release/gitwatch" "$pkgdir/usr/bin/gitwatch"

  # Install shell completions
  install -Dm644 <("target/release/gitwatch" completion bash) "$pkgdir/usr/share/bash-completion/completions/gitwatch"
  install -Dm644 <("target/release/gitwatch" completion fish) "$pkgdir/usr/share/fish/vendor_completions.d/gitwatch.fish"
  install -Dm644 <("target/release/gitwatch" completion zsh) "$pkgdir/usr/share/zsh/site-functions/_gitwatch"

  # Install man page
  install -Dm644 "docs/gitwatch.1" "$pkgdir/usr/share/man/man1/gitwatch.1"

  # Install license
  install -Dm644 "LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
